buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'me.ninethousand'
version = '0.1 Pre Alpha'
archivesBaseName = 'ninehack'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            args '--username', 'RITZY'

            workingDirectory project.file('run')

            property 'fml.coreMods.load', 'me.ninethousand.ninehack.mixin.MixinLoader'

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    compile('org.spongepowered:mixin:0.7.4-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    annotationProcessor('org.spongepowered:mixin:0.8.2:processor') {
        exclude module: 'gson'
    }

    compile 'com.github.olliem5:pace:1.3'
    compile 'com.github.CafeDevelopment:Venture:2.0-SNAPSHOT'
    compile group: 'club.minnced', name: 'java-discord-rpc', version: 'v2.0.2'

}

jar.manifest.attributes(
        'MixinConfigs': 'mixins.ninehack.json',
        'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'TweakOrder': 0,
        'FMLCorePluginContainsFMLMod': 'true',
        'FMLCorePlugin': 'me.ninethousand.ninehack.mixin.MixinLoader',
        'ForceLoadAsMod': 'true'
)

mixin {
    defaultObfuscationEnv 'searge'
    add sourceSets.main, 'refmap.ninehack.json'
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('com.github.olliem5:pace:1.3'))
        include(dependency('com.github.CafeDevelopment:Venture'))
        include(dependency('club.minnced:java-discord-rpc:v2.0.2'))

    }

    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'release'
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)